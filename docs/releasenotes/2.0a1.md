# Robotidy 2.0 alpha 1

Major release of Robotidy that brings new transformers and features. It focuses on bringing support for 
Robot Framework 5.0 new syntax such as ``BREAK``, ``CONTINUE``, ``RETURN`` statements or ``TRY EXCEPT`` block. 
Also `Robotidy` documentation got several improvements and extra examples.

## New Transformers
### `ReplaceReturns`
Replace return statements (such as ``[Return]`` setting or ``Return From Keyword keyword``) with ``RETURN`` statement ([#231](https://github.com/MarketSquare/robotframework-tidy/issues/231)).

Following code:

    *** Keywords ***
    Keyword
        Return From Keyword If    $condition    2
        Sub Keyword
        [Return]    1
    
    Keyword 2
        Run Keyword And Return If    ${var}==2  Keyword 2    ${var}
        Return From Keyword    ${arg}
    
    Keyword 3
        Run Keyword And Return    Keyword   ${arg}

will be transformed to:

    *** Keywords ***
    Keyword
        IF    $condition
            RETURN    2
        END
        Sub Keyword
        RETURN    1
    
    Keyword 2
        IF    ${var}==2
            RETURN    Keyword 2    ${var}
        END
        RETURN    ${arg}
    
    Keyword 3
        RETURN    Keyword   ${arg}

See https://robotidy.readthedocs.io/en/latest/transformers/ReplaceReturns.html for more examples

### `ReplaceBreakContinue`
Replace ``Continue For Loop`` and ``Exit For Loop`` keyword variants with ``CONTINUE`` and ``BREAK`` statements ([#241](https://github.com/MarketSquare/robotframework-tidy/issues/241)).

Following code:

    *** Keywords ***
    Keyword
        FOR    ${var}    IN  1  2
            Continue For Loop
            Continue For Loop If    $condition
            Exit For Loop
            Exit For Loop If    $condition
        END

will be transformed to:

    *** Keywords ***
    Keyword
        FOR    ${var}    IN  1  2
            CONTINUE
            IF    $condition
                CONTINUE
            END
            BREAK
            IF    $condition
                BREAK
            END
        END

See https://robotidy.readthedocs.io/en/latest/transformers/ReplaceBreakContinue.html for more examples.

### `InlineIf`
Replaces IF blocks with inline IF ([#230](https://github.com/MarketSquare/robotframework-tidy/issues/230)).

Following code::

    *** Test Cases ***
    Test
        IF    $condition1
            Keyword    argument
        END
        IF    $condition2
            ${var}  Keyword
        ELSE
            ${var}  Keyword 2
        END
        IF    $condition1
            Keyword    argument
            Keyword 2
        END

will be transformed to:

    *** Test Cases ***
    Test
        IF    $condition1    Keyword    argument
        ${var}    IF    $condition2    Keyword    ELSE    Keyword 2
        IF    $condition1
            Keyword    argument
            Keyword 2
        END

You can decide to not replace IF blocks containing ELSE or ELSE IF branches by setting ``skip_else`` to True.

See https://robotidy.readthedocs.io/en/latest/transformers/InlineIf.html for more examples.

## Changes in transformers
## `min_width` parameter
New ``min_width`` parameter was added to `AlignSettingsSection`, `AlignVariablesSection` and `AlignTestCases` ([#242](https://github.com/MarketSquare/robotframework-tidy/issues/242)).
It allows to set minimal width of data column instead of using width of the widest argument in the column.

Following code:

    *** Variables ***
    ${var}    a
    ${var2}  b

would be aligned to ``${var2}`` length since it's the longest token. If you wish to have fixed width you can use 
``min_width`` parameter:

```commandline
robotidy --configure AlignVariablesSection:min_width=15
```

With such configuration previous code will be transformed to:

    *** Variables ***
    ${var}         a
    ${var2}        b

## Sections only with comments
Previously sections that contained only comments and empty lines were removed by default by `DiscardEmptySections`. It 
could be changed by configuring `allow_only_comments` to `True`. Since it wasn't expected behaviour by most users now it 
works the opposite - now sections only with comments will be only removed by configuring `allow_only_comments` to `False` ([#235](https://github.com/MarketSquare/robotframework-tidy/issues/235)).


## Do not align variables with given types
New `skip_types` parameter in `AlignVariablesSection` which allows to not align variables of particular type ([#225](https://github.com/MarketSquare/robotframework-tidy/issues/225))

With following configuration:

```commandline
    robotidy --configure AlignVariablesSection:skip_types=dict
```

All variables except dictionaries will be aligned.

## Other
### Overwrite files with `--check` flag
It is now possible to force overwrite with ``--check`` and ``--overwrite`` flags ([#239](https://github.com/MarketSquare/robotframework-tidy/issues/239)).
Going over possible options:

Transform the files and return 0 even if there were files transformed.
```commandline
robotidy src
```

Don't transform the files and return 0 even if there would be files transformed
```commandline
robotidy --no-overwrite src
```

Don't transform the files and return 1 if there would be files transformed
```commandline
robotidy --check src
```

Transform the files and return 1 if there are files transformed
```commandline
robotidy --check --overwrite src
```

### User-friendly exceptions
Changed exceptions to be more user-friendly. Robotidy will try to recognize common mistakes. For unknown issues there is extra message with link to our bug board ([#250](https://github.com/MarketSquare/robotframework-tidy/issues/250))


### Changes to acceptance tests
Refactored Robotidy acceptance tests ([#248](https://github.com/MarketSquare/robotframework-tidy/issues/248)). 
